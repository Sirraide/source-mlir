<file> ::= <exprs>

/// ===========================================================================
///  Expressions
/// ===========================================================================
<exprs> ::= { <expr> | ";" }

<expr> ::= <expr-invoke>
         | <expr-proc>
         | <expr-literal>
         | <expr-if>
         | <expr-decl-ref>
         | <expr-access>
         | <expr-block>
         | <expr-prefix>
         | <expr-binary>
         | <expr-assert>
         | <expr-return>
         | <expr-defer>
         | <expr-cast>
         | <expr-while>
         | <expr-labelled>
         | <expr-loop-ctrl>
         | <decl>

<expr-assert>    ::= ASSERT <expr> [ ","  <expr> ]
<expr-invoke>    ::= <expr> [ "(" ] <expr> { "," <expr> } [ ")" ]
<expr-proc>      ::= <proc-extern> | <proc-named>
<expr-block>     ::= "{" <exprs> "}"
<expr-decl-ref>  ::= IDENTIFIER
<expr-access>    ::= <expr> "." IDENTIFIER
<expr-literal>   ::= INTEGER_LITERAL | STRING_LITERAL
<expr-prefix>    ::= <prefix-op> <expr>
<expr-binary>    ::= <expr> <binary-op> <expr>
<expr-if>        ::= IF <expr> <then> { ELIF <expr> <then> } [ ELSE <implicit-block> ]
<expr-return>    ::= RETURN [ <expr> ]
<expr-defer>     ::= DEFER <implicit-block>
<expr-cast>      ::= <expr> ( "as" | "as!" ) <type>
<expr-while>     ::= WHILE <expr> <do>
<expr-labelled>  ::= IDENTIFIER ":" <expr>
<expr-loop-ctrl> ::= ( BREAK | CONTINUE ) [ IDENTIFIER ]

<proc-extern>    ::= PROC IDENTIFIER <proc-signature>
<proc-named>     ::= PROC IDENTIFIER <proc-signature> <proc-body>
<proc-signature> ::= [ <proc-args> ] { <proc-attrs> } [ <proc-ret> ]
<proc-args>      ::= "(" <param-decl> { "," <param-decl> } ")"
<proc-ret>       ::= "->" <type>
<proc-body>      ::= <expr-block> | "=" <implicit-block>
<proc-attrs>     ::= EXTERN | NOMANGLE
<param-decl>     ::= <type> [ IDENTIFIER ]
<then>           ::= [ THEN ] <implicit-block>
<do>             ::= [ DO ] <implicit-block>
<implicit-block> ::= <expr> /// Syntactically any expression, but wrapped in an implicit block if it isnâ€™t already one.
<prefix-op>      ::= /// All prefix operators.
<binary-op>      ::= /// All binary operators.

/// ===========================================================================
///  Declarations
/// ==========================================================================
<decl>           ::= <proc-named> | <proc-extern> | <param-decl> | <var-decl>
<var-decl>       ::= <type> IDENTIFIER [ "=" <expr> ]

/// ===========================================================================
///  Types
/// ===========================================================================
<type>           ::= <type-prim> | <type-qualified>
<type-prim>      ::= INTEGER_TYPE | INT
<type-qualified> ::= <type> { <type-qual> }
<type-qual>      ::= "&"
